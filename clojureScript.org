#+title: Docs/ClojureScript

Guides:
- https://funcool.github.io/clojurescript-unraveled/
- https://lambdaisland.com/guides/clojure-repls/clojurescript-repls
- https://dawranliou.com/blog/vanilla-cljs/

* Getting started

** Vanilla setup

See https://clojurescript.org/guides/quick-start

Setup normal Clojure file structure.

In =deps.edn=:
: {:deps {org.clojure/clojurescript {:mvn/version "1.10.758"}}}

Compile with:
: $ clj -M -m cljs.main <options>

~--help~ To list compiler options:

- ~-c, --compile <ns>~ specify namespace/file to compile
  - ~--optimizations~ set compiler optimizations
- ~-t, --target <target>~ set compilation target, browser if none
- ~-d, --output-dir <path>~
- ~-re, --repl-env <env>~ set a different env for the REPL
- ~--repl~ launch a REPL after compilation
- ~--serve~ run the built-in web server (without REPL)

** Compile and run in Browser

To compile (followed by a REPL) and run in Browser:
: $ clj -M -m cljs.main --compile my.namespace --repl

- an =out/= directory is created that contains the compiled js code, but it is
  not optimized for production.
- now ClojureScript can be evaluated in the REPL and will be immediately
  rendered in the Browser

To compile with optimizations:
: $ clj -M -m cljs.main --optimizations advanced -c my.namespace

To just run the built-in simple web server:
: $ clj -M -m cljs.main --serve

** Run on Node.js

Enable source mapping:
: $ npm install source-map-support

To compile to Node.js:
: $ clj -M -m cljs.main --target node --output-to main.js -c my.namespace

- =main.js= file gets created, which can be executed with ~node main.js~
- no advanced optimizations needed here, since modern JS VM will optimize
  -> simple or none optimizations suffice

To open a REPL for Node.js:
: $ clj -M -m cljs.main --repl-env node

- append e.g. ~-m core~ to run -main function from =core.cljs=

** Setup with shadow-cljs

Setup Yarn:
: $ yarn init
: $ yarn set version berry
: $ echo 'nodeLinker: node-modules' >> .yarnrc.yml

Install Node dependency:
: $ yarn add --dev shadow-cljs
: $ yarn

Add a =shadow-cljs.edn= file:
#+begin_src clojure
{:source-paths ["src"]
 :dependencies [[binaryage/devtools "1.0.3"]
                [com.github.jpmonettas/flow-storm-inst "RELEASE"]]
 :builds       {:app {:target     :browser
                      :output-dir "public/js"
                      :asset-path "/js"
                      :modules    {:app {:entries [foo.core]}}
                      :devtools   {:after-load foo.core/mount-root}}}

 :dev-http     {3000 {:root "public"}}}
#+end_src

Builds, starts HTTP server and watches for changes:
: $ npx shadow-cljs watch <build-id>

Starts a REPL connected to nREPL port (uses JS runtime in Browser):
: $ npx shadow-cljs cljs-repl <build-id>

Starts flowstorm debugger:
: $ clj -Sforce -Sdeps '{:deps {com.github.jpmonettas/flow-storm-dbg {:mvn/version "RELEASE"}}}' -X flow-storm.debugger.main/start-debugger :port 9000 :repl-type :shadow :build-id :app

*** shadow-cljs & deps.edn

https://shadow-cljs.github.io/docs/UsersGuide.html#deps-edn

Do not use ~:source-paths~ and ~:dependencies~ in =shadow-cljs.edn= when working
with =deps.edn=.

Dependency conflics need to be resolved manually.

Set ~:npm-deps {:install false}~ to prevent shadow-cljs from automatically
installing npm deps.
- https://github.com/thheller/shadow-cljs/issues/800#issuecomment-725716087


** Dependencies / Imports
See:
- https://clojurescript.org/reference/dependencies
- https://shadow-cljs.github.io/docs/UsersGuide.html#_using_npm_packages
- https://www.digitalocean.com/community/tutorials/js-modules-es6

* Google Closure Library
:PROPERTIES:
:CUSTOM_ID: goog
:END:

(see [[file:google-closure.org][google-closure.org]])

* Interop

** Types

Strings are JS strings.

Clojures datastructures can be converted to JS objects with the ~#js~ tag.
- not recursive, inner values wonâ€™t convert!
- ~clj->js~ converts recursively
- use ~js->clj~ to convert back

#+begin_src clojurescript
(def obj-from-map #js {:a 1 :b "two"})
obj-from-map ;=> #js {:a 1, :b "two"}
;; equivalent to:
(def obj (js-obj "a" 1 "b" "two"))
obj ;=> #js {:a 1, :b "two"}

(js/console.log obj-from-map) ; (browser console)=> {a: 1, b: 'two'}

(js/console.log #js ["a" :b 'c 123]) ; (brc)=> ['a', {<clj>}, {<clj>}, 123]

(js/console.log (clj->js ["a" :b 'c 123])) ; (brc)=> ['a', 'b', 'c', 123]

; keywords and symbols remain strings!
(js->clj (clj->js ["a" :b 'c 123])) ;=> {"a" 1, "b" "two"}
; in maps, this option converts back to keywords:
(js->clj (clj->js {:a 1 :b "two"}) :keywordize-keys true) ;=> {:a 1, :b "two"}
; but it wonâ€™t work with keywords in vectors
#+end_src

*** Type Hints

Use in definitions, function arguments and let-bindings to tell the
compiler that the name binds to a javascript object of some kind.

General js object:
: ^js x
More specific:
: ^js/Object x
: ^js/Array x
: ^js/HTMLDocument x
: ^js/MutationObserver x

Examples:
#+begin_src clojurescript
(defn first-match [^js/Array selectors] ;; cannot be inferred
  (let [result (atom js/undefined)]
    (.forEach selectors
              (fn [s]
                (when-let [elem (.. js/document (querySelector s))]
                  (reset! result elem))))
    result))

(def ^js/Array arr (array ".xyz" "#root")) ;; not necessary, can be inferred

(let [^js elem   (first-match arr) ;; who knows what this returns?
      ^js/Node p (.. elem (querySelector "p"))] ;; can be inferred?
  (.-textContents p))
#+end_src

*** Objects

Define JS objects with ~js-obj~ or ~#js {â€¦}~ .
- keys must be strings (as opposed to Clojure maps)
  - when using ~#js~, keywords in the input map get converted to strings
- member-access with ~(.-x obj)~ notation
  - compiles to ~obj.x~
  - *Note:* properties might get renamed during [[https://github.com/clojure/clojurescript/wiki/Advanced-Compilation][advanced optimizations]],
    which breaks with direct access
    - ~aget~ might work (and compiles to ~obj["x"]~), but is explicitly
      [[https://cljs.github.io/api/cljs.core/aget][documented]] for native arrays only
    - ~goog.object/get~ and (multi-arity) ~goog.object/getValueByKeys~ from
      [[#goog][Google Closure Library]] or ~oget~ from [[#cljs-oops][cljs-oops]] solve this issue
      without relying on externs
      - see [[https://github.com/binaryage/cljs-oops?tab=readme-ov-file#motivation][explanation]] for arguments against using externs files
      - for using externs, there is now [[https://clojurescript.org/guides/externs#externs-inference][externs inference]]
      - also see [[http://lukevanderhart.com/2011/09/30/using-javascript-and-clojurescript.html][Using JavaScript libraries in ClojureScript]] (2011)
- set properties with ~aset~

#+begin_src clojurescript
(def obj (js-obj "x" 1 "y" 2))
obj ;=> #js {:x 1, :y 2}

;; literal representation with reader macro:
#js {:x 1 :y 2}

;; Member access:
(aget ^js obj "y") ;=> 2
(.-y ^js obj) ;=> 2

;; Set/create property (mutation):
(aset ^js obj "z" 3) ;=> 3
obj  ;=> #js {:x 1, :y 2, :z 3}

;; With reader macro:
(.-foo #js {:foo "bar"}) ;=> "bar"
(aget #js {:foo "bar"} "foo") ;=> "bar" (equivalent)
#+end_src

*** Arrays

Define JS arrays with ~array~, ~#js [â€¦]~, ~make-array~ or ~js/Array.from~ .
- use ~aget~ / ~aset~ for member access/mutation (like with JS objects)
- use native ~.forEach~ to iterate for better performance in large arrays
- use ~aclone~ to clone an array (shallow copy)

#+begin_src clojurescript
(def arr (array 1 2 3))
arr ;=> #js [1 2 3]

;; Index access:
(aget arr 1) ;=> 2
(aget ["a" "b" "c"] 1) ;=> nil (wonâ€™t work with vectors!)

;; Set/create element (mutation):
(aset arr 3 "foo") ;=> "foo"
arr ;=>  #js [1 2 3 "foo"]

;; To convert js objects to js Arrays:
(js/Array.from "Hey") ;=> #js ["H" "e" "y"]

;; Iterating over a js Array:
(.forEach arr
          (fn [x]
            (js/console.log x)))
#+end_src

*** Classes
At the moment (05/2024), ~class~ definitions are not supported in Cljs.
~deftype~ and ~defrecord~ should be used to write class-like behaviour.

However, *shadow-cljs* has a ~defclass~ macro that generates an actual ~class~
without trying to emulate it in any way. However, some features are not
yet supported (e.g. defining getters/setters).
- [[https://clojureverse.org/t/modern-js-with-cljs-class-and-template-literals/7450][Modern JS with CLJS: class and template literals]] (ClojureVerse)
- [[https://github.com/thheller/shadow-cljs/blob/master/src/main/shadow/cljs/modern.cljc][sourcecode for the macro]]
- more complex examples: [[https://github.com/thheller/shadow-experiments/blob/master/src/main/shadow/experiments/grove/components.cljs#L151][here]] and [[https://github.com/thheller/shadow-experiments/blob/master/src/main/shadow/experiments/grove/ui/vlist.cljs#L26][here]]
  
Classes are required to write web components.
- [[https://github.com/raystubbs/zero][raystubbs/zero]] is a Cljs library to simplify building web components

Example:
#+begin_src clojurescript
(ns my-web-component
  (:require [shadow.cljs.modern :refer (defclass)]))

(defclass MyWebComponent
  (extends js/HTMLElement)

  (field ^Object root)

  (constructor
   [this]
   (super))

  ;; This method gets called each time the element gets added to the document:
  Object
  (connectedCallback
   [this]
   (let [shadow (.. this (attachShadow #js {:mode "open"}))
         elem (js/document.createElement "div")]
     (aset elem "textContent"
           (.. this (getAttribute "someProp")))
     (set! (.-root this) elem)
     (.. shadow (appendChild elem))))

  ;; This method gets called when any of the observed attributes change:
  (attributeChangedCallback
   [this attr-name old-v new-v]
   (js/console.log (str "Attribute " attr-name " has changed."))
   (js/console.log (str "old val: `" old-v "` , new val: `" new-v "`"))
   (when root
     (aset root "textContent" new-v))))

;; To respond to attribute changes, set them as observedAttributes:
;; NOTE: this is a workaround to set static properties in classes
(set! (.-observedAttributes MyWebComponent) #js ["someProp"])

;; Register the component as a custom element
(js/window.customElements.define "my-web-component" MyWebComponent)
#+end_src

** JS methods

Prefix ~.~ to call a method from a JS object: ~(.f obj)~
- use ~..~ syntax sugar to call chained methods: ~(.. obj f1 (f2 "arg"))~
- call as constructor with a leading ~.~ : ~(MyClass. "arg")~

#+begin_src clojurescript
(.toUpperCase "Hello world!")

(.. "Hello world!" (charCodeAt 7) (toString 16) toUpperCase)

;; Create object from a constructor (like â€œnewâ€ in JS):
(js/Date. "2016-05-19") ;=> #inst "2016-05-19T00:00:00.000-00:00"

;; To use apply in calling a method, use the .apply method of function objects:
;; See http://clojurescriptmadeeasy.com/blog/how-to-apply-with-the-console-api.html
(.apply (.-methodName my-obj) my-obj args)

#+end_src

** â€œthisâ€ keyword

Use ~this-as~ macro to bind this to a variable.

Example usage:

#+begin_src clojurescript
(defn click-handler []
  (this-as b
     (set! (.-innerHTML b) (inc (long (.-innerHTML b))))))

(let [button (.createElement js/document "button")]
  (set! (.-innerHTML button) "0")
  (set! (.-onclick button) click-handler)
  (.. js/document -body (appendChild button)))
#+end_src

** JS properties

Prefix ~.-~ to access properties of JS objects: ~(.-prop obj)~
- use ~..~ syntax sugar to access chained properties: ~(.. obj -prop1 -prop2)~

Use ~set!~ to set properties: ~(set! (.-prop obj) "new val")~
- mutates the object
- also adds the property if not already set
- ~aset~ will also work: ~(aset obj "prop" "new val")~ (also mutates object)

#+begin_src clojurescript
;; Get properties:
(.-length "Hello world!")

;; Set properties:
(set! (.. js/document (getElementById "app") -innerHTML) "Hello world!")

;; Chained properties:
(.. js/document -body -lastChild -innerHTML -length)
#+end_src

** Global variables

~js~ is ClojureScripts namespace for global JavaScript variables.
- e.g. ~js/document~, ~js/window~, ~js/console.log~, ~js/Node~

#+begin_src clojurescript
(js/console.log "Hey ho") ; appears in browser console
#+end_src

To create a new variable in the global namespace, use ~set!~ on ~js/x~:
- can be accessed in the browser console
#+begin_src clojurescript
(set! js/newVar 3)
js/newVar ;=> 3
#+end_src

Global variables can be looked up from ~js~ namespace like in JS:
#+begin_src clojurescript
(js/document.body.lastChild.innerHTML.charAt 7)
#+end_src

** Error Handling

Example:
#+begin_src clojurescript
(defonce maybe-x (atom nil))

(try
  (if-let [x @maybe-x]
    x
    (throw (js/Error. "No x!")))
  (catch js/Error e
    (js/console.error e))
  (finally
    (js/console.log "Anyways.")))
#+end_src

* Snippets
** Async

#+begin_src clojurescript
(def myvar* (atom nil))

(def wait-for-myvar
  (js/Promise.
   (fn [resolve reject]
     (let [interval (atom nil)
           attempts (atom 0)]
       (reset! interval
               (js/setInterval
                (fn []
                  (when-let [val @myvar*]
                    (js/clearInterval @interval)
                    (resolve val))
                  (when (> @attempts 10)
                    (reject))
                  (swap! attempts inc))
                100))))))

(.then wait-for-myvar
       (fn [val]
         (js/console.log "ðŸ˜‹ obtained " val))
       (fn [_]
         (js/console.error "ðŸ«¥ failed to obtain value")))

(reset! myvar* 42)
#+end_src

** MutationObserver

#+begin_src clojurescript
(defonce iframe (atom nil))

(defn make-mounted-observer-callback
  [added-node-callback]
  (js/MutationObserver.
   (fn [^js/Array mutations ^js/MutationObserver observer]
     (.forEach
      mutations
      (fn [^js/MutationRecord mutation]
        (when (= (.-type mutation) "childList")
          (.forEach
           (.-addedNodes mutation)
           (fn [^js/Node node]
             (added-node-callback node observer)))))))))

(def mounted-iframe-observer
  (make-mounted-observer-callback
   (fn [^js/Node node ^js/MutationObserver observer]
     (when (and (= (.-nodeName node) "DIV")
                (> (.. node -children -length) 0))
       (reset! iframe (.querySelector node "iframe"))
       (when @iframe
         (.disconnect observer))))))

(.observe mounted-iframe-observer
          (.. js/document (getElementById "container"))
          #js {:childList true
               :subtree true})
#+end_src

* .cljc (Reader Conditionals)

Reader Conditionals allow different Clojure dialects to share common code (e.g. to target both Clojure and ClojureScript).
- mostly platform independent
- previous format was .cljx which is now deprecated.
- can be used for Spec definition files across dialects

They can be manipulated like ordinary Clojure expressions.

Guides:
- https://github.com/clojure/clojurescript/wiki/Using-cljc
- https://clojure.org/guides/reader_conditionals

** Standard reader conditional

Similar to ~cond~.

#+begin_src clojurescript
#?(:clj  (Clojure expression)
   :cljs (ClojureScript expression)
   :cljr (Clojure CLR expression)
   :default (fallthrough expression))
#+end_src

- if no tags match and no ~:default~, Reader will read nothing (not even ~nil~)

** Splicing reader conditional

Splices lists into the containing form.

#+begin_src clojurescript
(defn build-list []
  (list #?@(:clj  [5 6 7 8]
            :cljs [1 2 3 4])))

;; Clojure reader would read this as:
(defn build-list []
  (list 5 6 7 8))
#+end_src

* Libraries
** cljs-oops
:PROPERTIES:
:CUSTOM_ID: cljs-oops
:END:
ClojureScript macros for convenient native Javascript object access.
- [[https://github.com/binaryage/cljs-oops]]
